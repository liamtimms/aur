_pkgname=vispy
pkgname=python-$_pkgname
pkgver=v0.6.4
pkgrel=3
pkgdesc='A high-performance interactive 2D/3D data visualization library.'
arch=('any')
url='http://vispy.org'
license=('BSD')
depends=('python-numpy')
optdepends=('pyside2: a possible backend'
            'python-pyqt5: a possible backend')
source=("git://github.com/vispy/vispy.git#tag=$pkgver"
        "git://github.com/vispy/vispy.js.git")
md5sums=('SKIP'
         'SKIP')

prepare() {
  cd "$srcdir/vispy"
  git submodule init
  git config submodule.js.url $srcdir/vispy.js
  git submodule update
}

build() {
  cd "$srcdir/vispy"
  python setup.py build
}

package() {
  cd "$srcdir/vispy"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

